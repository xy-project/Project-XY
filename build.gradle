buildscript {
    repositories
            {
                mavenCentral()
                maven {
                    name = "forge"
                    url = "http://files.minecraftforge.net/maven"
                }
                maven {
                    name = "sonatype"
                    url = "https://oss.sonatype.org/content/repositories/snapshots/"
                }
            }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT"
    }
}
ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}


apply plugin: "scala"
apply plugin: "forge"

group = "com.projectxy"
archivesBaseName = "Project-XY"
version = config.mc_version + "-" + config.version

minecraft {
    version = config.mc_version + "-" + config.forge_version
    assetDir = "run/assets"
}

processResources {
    inputs.property "version", config.version
    inputs.property "mc_version", config.mc_version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include '*.info'
        // replace version and mcversion
        expand 'version': project.version, 'mc_version': project.minecraft.version
    }
}

// Add in an mcp named jar, for those who wish to run in a development environment (assuming mcp naming matches)
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
    version = project.version
}

jar {
    classifier = 'universal'
}

artifacts {
    archives deobfJar
}

repositories {
    maven {
        name 'CB Maven FS'
        url "http://chickenbones.net/maven/"
    }
}

dependencies {
    compile "codechicken:NotEnoughItems:${config.mc_version}-${config.nei_version}:dev"
    compile "codechicken:CodeChickenLib:${config.mc_version}-${config.ccl_version}:dev"
    compile "codechicken:CodeChickenCore:${config.mc_version}-${config.ccc_version}:dev"
    compile "codechicken:ForgeMultipart:${config.mc_version}-${config.fmp_version}:dev"
}
